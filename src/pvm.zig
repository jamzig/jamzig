const std = @import("std");
const Allocator = std.mem.Allocator;
const Instruction = @import("./pvm/instruction.zig").Instruction;
const Program = @import("./pvm/program.zig").Program;
const Decoder = @import("./pvm/decoder.zig").Decoder;
const InstructionWithArgs = @import("./pvm/decoder.zig").InstructionWithArgs;
const updatePc = @import("./pvm/utils.zig").updatePc;
const trace = @import("tracing.zig").scoped(.pvm);

pub const PVMErrorData = union(enum) {
    // when a page fault occurs we whould return the lowest address which caused the fault
    page_fault: u32,
    host_call: u32,
};

pub const PMVHostCallResult = union(enum) {
    play,
    page_fault: u32,
};

const PMVHostCallFn = fn (*i64, *[13]u32, []PVM.PageMap) PMVHostCallResult;

pub const PVM = struct {
    allocator: Allocator,
    program: Program,
    decoder: Decoder,
    registers: [13]u32,
    pc: u32,
    page_map: []PageMap,
    gas: i64,
    error_data: ?PVMErrorData,
    host_call_map: std.AutoHashMap(u32, *const PMVHostCallFn),

    pub fn hostCall(self: *PVM, host_call_idx: u32) !void {
        const span = trace.span(.host_call);
        defer span.deinit();
        span.debug("Executing host call {d}", .{host_call_idx});

        if (self.host_call_map.get(host_call_idx)) |host_func| {
            var gas_c = self.gas;
            var registers_c = self.registers;
            const page_map_c = try self.clonePageMap();

            span.trace("Host call state - Gas: {d}, Registers: {any}", .{ gas_c, registers_c });

            switch (host_func(&gas_c, &registers_c, page_map_c)) {
                .play => {
                    span.debug("Host call completed successfully", .{});
                },
                .page_fault => |address| {
                    span.err("Host call resulted in page fault at address 0x{X:0>8}", .{address});
                    self.freePageMap(page_map_c);
                    self.error_data = .{ .page_fault = address };
                    return error.MemoryPageFault;
                },
            }

            // Update state after successful host call
            self.gas = gas_c;
            self.registers = registers_c;
            self.freePageMap(self.page_map);
            self.page_map = page_map_c;

            span.trace("Updated state - Gas: {d}, Registers: {any}", .{ self.gas, self.registers });
        } else {
            span.err("Non-existent host call index: {d}", .{host_call_idx});
            return error.NonExistentHostCall;
        }
    }

    pub fn registerHostCall(self: *PVM, host_call_idx: u32, host_func: PMVHostCallFn) !void {
        const span = trace.span(.register_host_call);
        defer span.deinit();

        try self.host_call_map.put(host_call_idx, host_func);
        span.debug("Registered host call handler for index {d}", .{host_call_idx});
    }

    pub const PageMap = struct {
        address: u32,
        length: u32,
        is_writable: bool,
        data: []align(8) u8,
    };

    pub const PageMapConfig = struct {
        address: u32,
        length: u32,
        is_writable: bool,
    };

    pub const MemoryChunk = struct {
        address: u32,
        contents: []u8,
    };

    pub const Status = enum {
        play, // The program is still running ready for next instruction execution
        trap, // Trap is likely used to represent a special kind of control flow change
        panic, // An exceptional condition or error occurred, causing an immediate halt.
        halt, // The program terminated normally.
        out_of_gas, // The program consumed its allocated gas, and further execution is halted
        page_fault, // The program tried to access a memory address that was not available or permitted.
        host_call, // The program made a request to interact with a host environment
    };

    pub fn init(allocator: Allocator, raw_program: []const u8, initial_gas: i64) !PVM {
        const span = trace.span(.init);
        defer span.deinit();

        span.debug("Initializing PVM with {d} bytes of program data, {d} initial gas", .{ raw_program.len, initial_gas });

        const program = try Program.decode(allocator, raw_program);
        span.debug("Program decoded - code size: {d}, mask size: {d}", .{ program.code.len, program.mask.len });

        span.trace("{s}", .{program});

        return PVM{
            .allocator = allocator,
            .program = program,
            .error_data = null,
            .decoder = Decoder.init(program.code, program.mask),
            .registers = [_]u32{0} ** 13,
            .pc = 0,
            .page_map = &[_]PageMap{},
            .gas = initial_gas,
            .host_call_map = std.AutoHashMap(u32, *const PMVHostCallFn).init(allocator),
        };
    }

    pub fn deinit(self: *PVM) void {
        const span = trace.span(.deinit);
        defer span.deinit();

        span.debug("Cleaning up PVM resources", .{});

        self.program.deinit(self.allocator);
        for (self.page_map) |page| {
            self.allocator.free(page.data);
        }
        self.allocator.free(self.page_map);
        self.host_call_map.deinit();
        self.* = undefined;
    }

    pub fn setPageMap(self: *PVM, new_page_map: []const PageMapConfig) !void {
        const span = trace.span(.set_page_map);
        defer span.deinit();

        span.debug("Setting new page map with {d} pages", .{new_page_map.len});

        // Free existing pages
        for (self.page_map) |page| {
            self.allocator.free(page.data);
        }
        self.allocator.free(self.page_map);

        // Allocate new page map
        self.page_map = try self.allocator.alloc(PageMap, new_page_map.len);

        // Initialize new pages
        for (new_page_map, 0..) |config, i| {
            span.debug("Initializing page {d}: address=0x{X:0>8}, length={d}, writable={}", .{ i, config.address, config.length, config.is_writable });

            self.page_map[i] = PageMap{
                .address = config.address,
                .length = config.length,
                .is_writable = config.is_writable,
                .data = try self.allocator.allocWithOptions(u8, config.length, 8, null),
            };
        }
    }

    pub fn debugWriteDecompiled(self: *PVM, writer: anytype) !void {
        const span = trace.span(.write_decompiled);
        defer span.deinit();

        const decoder = Decoder.init(self.program.code, self.program.mask);
        var pc: u32 = 0;

        while (pc < self.program.code.len) {
            const i = try decoder.decodeInstruction(pc);
            try writer.print("{d:0>4}: {any}\n", .{ pc, i });
            pc += i.skip_l() + 1;
        }
    }

    // TODO: rename to debugPrintDecompiled
    pub fn decompilePrint(self: *PVM) void {
        const span = trace.span(.decompile_print);
        defer span.deinit();

        self.debugWriteDecompiled(std.io.getStdErr().writer()) catch |err| {
            span.err("Failed to print decompiled code: {}", .{err});
        };
    }

    pub fn debugWriteRegisters(self: *const PVM, writer: anytype) !void {
        const span = trace.span(.write_registers);
        defer span.deinit();

        const reg_names = [_][]const u8{ "zero", "ra", "sp", "gp", "tp", "t0", "t1", "t2", "s0", "s1", "a0", "a1", "a2" };
        try writer.writeAll("\nRegister values:\n");
        for (self.registers, 0..) |reg, i| {
            try writer.print("r{d:<2} ({s:<4}): 0x{X:0>8} ({d})\n", .{
                i, reg_names[i], reg, reg,
            });
        }
    }

    pub fn debugPrintRegisters(self: *const PVM) void {
        const span = trace.span(.print_registers);
        defer span.deinit();

        self.debugWriteRegisters(std.io.getStdErr().writer()) catch |err| {
            span.err("Failed to print registers: {}", .{err});
        };
    }

    fn getGasCost(instruction: InstructionWithArgs) u32 {
        return switch (instruction.instruction) {
            .trap => 0,
            .load_u8 => 1,
            else => 1,
        };
    }

    pub fn innerRunStep(self: *PVM) !void {
        const span = trace.span(.execute_step);
        defer span.deinit();

        const i = try self.decoder.decodeInstruction(self.pc);
        span.debug("Executing instruction at PC={X:0>4}: {any}", .{ self.pc, i.instruction });
        span.trace("Full instruction with args: {any}", .{i});

        const gas_cost = getGasCost(i);
        if (self.gas < gas_cost) {
            span.err("Out of gas: required={d}, remaining={d}", .{ gas_cost, self.gas });
            return error.OutOfGas;
        }

        self.gas -= gas_cost;
        span.debug("Gas remaining after cost: {d}", .{self.gas});

        const execution_span = span.child(.instruction_execution);
        defer execution_span.deinit();

        self.pc = try updatePc(self.pc, self.executeInstruction(i) catch |err| {
            execution_span.err("Instruction execution failed: {any}", .{err});
            // Charge one extra gas for error handling
            self.gas -= switch (err) {
                error.JumpAddressHalt => 0,
                error.JumpAddressZero => 0,
                else => 1,
            };
            return err;
        });

        execution_span.debug("Updated PC to {X:0>4}", .{self.pc});

        if (execution_span.traceLogLevel()) {
            const register_span = execution_span.child(.registers);
            defer register_span.deinit();
            // Only construct register state string if trace is enabled
            for (self.registers, 0..) |reg, idx| {
                register_span.trace("r{d}=0x{X:0>8}", .{ idx, reg });
            }
        }
    }

    pub fn runStep(self: *PVM) Status {
        const span = trace.span(.run_step);
        defer span.deinit();

        const result = self.innerRunStep();

        if (result) {
            span.debug("Step completed successfully", .{});
            return .play;
        } else |err| {
            const status = switch (err) {
                error.Trap => Status.panic,
                error.OutOfGas => Status.out_of_gas,
                error.NonExistentHostCall => Status.panic,
                error.OutOfMemory => Status.panic,
                error.MemoryWriteProtected => Status.page_fault,
                error.MemoryPageFault => Status.page_fault,
                error.MemoryAccessOutOfBounds => Status.page_fault,
                error.JumpAddressHalt => Status.halt,
                error.JumpAddressZero => Status.panic,
                error.JumpAddressOutOfRange => Status.panic,
                error.JumpAddressNotAligned => Status.panic,
                error.JumpAddressNotInBasicBlock => Status.panic,
                error.InvalidInstruction => Status.panic,
                error.PcUnderflow => Status.panic,
                // else => @panic("Unknown error"),
            };
            span.info("Step resulted in status: {}", .{status});
            return status;
        }
    }

    pub fn run(self: *PVM) Status {
        const span = trace.span(.run);
        defer span.deinit();
        span.debug("Starting program execution", .{});

        while (true) {
            const status = self.runStep();
            switch (status) {
                .play => continue,
                else => {
                    span.info("Program finished with status: {}", .{status});
                    return status;
                },
            }
        }
    }

    const PcOffset = i32;
    /// executes the instruction and returns the offset to add to the program counter
    fn executeInstruction(self: *PVM, i: InstructionWithArgs) !PcOffset {
        switch (i.instruction) {
            .trap => {
                // Halt the program
                return error.Trap;
            },
            .load_imm => {
                // Load immediate value into register
                const args = i.args.one_register_one_immediate;
                self.registers[args.register_index] = args.immediate;
            },
            .jump => {
                // Jump to offset
                const args = i.args.one_offset;
                return args.offset;
            },
            .add_imm => {
                // Add immediate value to register
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] =
                    self.registers[args.second_register_index] +%
                    args.immediate;
            },
            .move_reg => {
                const args = i.args.two_registers;
                self.registers[args.first_register_index] = self.registers[args.second_register_index];
            },
            .fallthrough => {
                // Do nothing, just move to the next instruction
            },
            .add => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] =
                    self.registers[args.first_register_index] +%
                    self.registers[args.second_register_index];
            },
            .@"and" => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] & self.registers[args.second_register_index];
            },
            .and_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] & args.immediate;
            },
            .xor_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] ^ args.immediate;
            },
            .or_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] | args.immediate;
            },
            .mul_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] *% args.immediate;
            },
            .mul_upper_s_s_imm => {
                const args = i.args.two_registers_one_immediate;
                const result: i64 = @as(i64, @intCast(@as(i32, @bitCast(self.registers[args.second_register_index])))) * @as(i64, @intCast(args.immediate));
                self.registers[args.first_register_index] = @as(u32, @bitCast(@as(i32, @intCast(result >> 32))));
            },
            .mul_upper_u_u_imm => {
                const args = i.args.two_registers_one_immediate;
                const result: u64 = @as(u64, self.registers[args.second_register_index]) * @as(u64, args.immediate);
                self.registers[args.first_register_index] = @as(u32, @intCast(result >> 32));
            },
            .set_lt_u_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = if (self.registers[args.second_register_index] < args.immediate) 1 else 0;
            },
            .set_lt_s_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = if (@as(i32, @bitCast(self.registers[args.second_register_index])) < @as(i32, @bitCast(args.immediate))) 1 else 0;
            },
            .shlo_l_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] << @intCast(args.immediate % 32);
            },
            .shlo_l_imm_alt => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = args.immediate << @intCast(self.registers[args.second_register_index] % 32);
            },
            .shlo_r_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = self.registers[args.second_register_index] >> @intCast(args.immediate % 32);
            },
            .shlo_r_imm_alt => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = args.immediate >> @intCast(self.registers[args.second_register_index] % 32);
            },
            .shar_r_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = @bitCast(
                    @as(i32, @bitCast(self.registers[args.second_register_index])) >> @intCast(args.immediate % 32),
                );
            },
            .shar_r_imm_alt => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = @bitCast(
                    @as(i32, @bitCast(args.immediate)) >> @intCast(self.registers[args.second_register_index] % 32),
                );
            },
            .neg_add_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = args.immediate -% self.registers[args.second_register_index];
            },
            .set_gt_u_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = if (self.registers[args.second_register_index] > args.immediate) 1 else 0;
            },
            .set_gt_s_imm => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = if (@as(i32, @bitCast(self.registers[args.second_register_index])) > @as(i32, @bitCast(args.immediate))) 1 else 0;
            },
            .cmov_iz_imm => {
                const args = i.args.two_registers_one_immediate;
                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.first_register_index] = args.immediate;
                }
            },
            .cmov_nz_imm => {
                const args = i.args.two_registers_one_immediate;
                if (self.registers[args.second_register_index] != 0) {
                    self.registers[args.first_register_index] = @bitCast(args.immediate);
                }
            },
            .sub => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] -% self.registers[args.second_register_index];
            },
            .xor => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] ^ self.registers[args.second_register_index];
            },
            .@"or" => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] | self.registers[args.second_register_index];
            },
            .mul => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] *% self.registers[args.second_register_index];
            },
            .mul_upper_s_s => {
                const args = i.args.three_registers;
                const result = @as(i64, @intCast(@as(i32, @bitCast(self.registers[args.first_register_index])))) * @as(i64, @intCast(@as(i32, @bitCast(self.registers[args.second_register_index]))));
                self.registers[args.third_register_index] = @as(u32, @bitCast(@as(i32, @intCast(result >> 32))));
            },
            .mul_upper_u_u => {
                const args = i.args.three_registers;
                const result = @as(u64, self.registers[args.first_register_index]) * @as(u64, self.registers[args.second_register_index]);
                self.registers[args.third_register_index] = @intCast(result >> 32);
            },
            .mul_upper_s_u => {
                const args = i.args.three_registers;
                const result = @as(i64, @intCast(@as(i32, @bitCast(self.registers[args.first_register_index])))) * @as(i64, self.registers[args.second_register_index]);
                self.registers[args.third_register_index] = @as(u32, @bitCast(@as(i32, @intCast(result >> 32))));
            },
            .div_u => {
                const args = i.args.three_registers;
                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.third_register_index] = 0xFFFFFFFF;
                } else {
                    self.registers[args.third_register_index] = @divTrunc(self.registers[args.first_register_index], self.registers[args.second_register_index]);
                }
            },
            .div_s => {
                const args = i.args.three_registers;
                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.third_register_index] = 0xFFFFFFFF;
                } else if (self.registers[args.first_register_index] == 0x80000000 and
                    self.registers[args.second_register_index] == 0xFFFFFFFF)
                {
                    self.registers[args.third_register_index] = 0x80000000;
                } else {
                    self.registers[args.third_register_index] = @as(u32, @bitCast(
                        @divTrunc(
                            @as(i32, @bitCast(self.registers[args.first_register_index])),
                            @as(i32, @bitCast(self.registers[args.second_register_index])),
                        ),
                    ));
                }
            },
            .rem_u => {
                const args = i.args.three_registers;
                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.third_register_index] = self.registers[args.first_register_index];
                } else {
                    self.registers[args.third_register_index] = self.registers[args.first_register_index] % self.registers[args.second_register_index];
                }
            },
            .rem_s => {
                const args = i.args.three_registers;

                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.third_register_index] = self.registers[args.first_register_index];
                } else if (self.registers[args.first_register_index] == 0x80000000 and
                    self.registers[args.second_register_index] == 0xFFFFFFFF)
                {
                    self.registers[args.third_register_index] = 0x00;
                } else {
                    self.registers[args.third_register_index] = @as(
                        u32,
                        @bitCast(
                            @rem(
                                @as(
                                    i32,
                                    @bitCast(self.registers[args.first_register_index]),
                                ),
                                @as(
                                    i32,
                                    @bitCast(self.registers[args.second_register_index]),
                                ),
                            ),
                        ),
                    );
                }
            },
            .set_lt_u => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = if (self.registers[args.first_register_index] < self.registers[args.second_register_index]) 1 else 0;
            },
            .set_lt_s => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = if (@as(i32, @bitCast(self.registers[args.first_register_index])) < @as(i32, @bitCast(self.registers[args.second_register_index]))) 1 else 0;
            },
            .shlo_l => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] << @intCast(self.registers[args.second_register_index] & 0x1F);
            },
            .shlo_r => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = self.registers[args.first_register_index] >> @intCast(self.registers[args.second_register_index] & 0x1F);
            },
            .shar_r => {
                const args = i.args.three_registers;
                self.registers[args.third_register_index] = @as(u32, @bitCast(@as(i32, @bitCast(self.registers[args.first_register_index])) >> @intCast(self.registers[args.second_register_index] & 0x1F)));
            },
            .cmov_iz => {
                const args = i.args.three_registers;
                if (self.registers[args.second_register_index] == 0) {
                    self.registers[args.third_register_index] = self.registers[args.first_register_index];
                }
            },
            .cmov_nz => {
                const args = i.args.three_registers;
                if (self.registers[args.second_register_index] != 0) {
                    self.registers[args.third_register_index] = self.registers[args.first_register_index];
                }
            },
            .ecalli => {
                const args = i.args.one_immediate;
                try self.hostCall(args.immediate);
            },
            .store_imm_u8 => {
                const args = i.args.two_immediates;
                try self.storeMemory(args.first_immediate, @intCast(args.second_immediate), 1);
            },
            .store_imm_u16 => {
                const args = i.args.two_immediates;
                try self.storeMemory(args.first_immediate, @intCast(args.second_immediate), 2);
            },
            .store_imm_u32 => {
                const args = i.args.two_immediates;
                try self.storeMemory(args.first_immediate, args.second_immediate, 4);
            },
            .jump_ind => {
                const args = i.args.one_register_one_immediate;
                return self.djump(self.registers[args.register_index] +% args.immediate);
            },
            .load_u8 => {
                const args = i.args.one_register_one_immediate;
                self.registers[args.register_index] = try self.loadMemory(args.immediate, 1);
            },
            .load_i8 => {
                const args = i.args.one_register_one_immediate;
                const value = try self.loadMemory(args.immediate, 1);
                self.registers[args.register_index] = @as(u32, @bitCast(@as(i32, @intCast(@as(i8, @bitCast(@as(u8, @truncate(value))))))));
            },
            .load_u16 => {
                const args = i.args.one_register_one_immediate;
                self.registers[args.register_index] = try self.loadMemory(args.immediate, 2);
            },
            .load_i16 => {
                const args = i.args.one_register_one_immediate;
                const value = try self.loadMemory(args.immediate, 2);
                self.registers[args.register_index] = @as(
                    u32,
                    @bitCast(
                        @as(i32, @intCast(
                            @as(i16, @bitCast(
                                @as(u16, @truncate(value)),
                            )),
                        )),
                    ),
                );
            },
            .load_u32 => {
                const args = i.args.one_register_one_immediate;
                self.registers[args.register_index] = try self.loadMemory(args.immediate, 4);
            },
            .store_u8 => {
                const args = i.args.one_register_one_immediate;
                try self.storeMemory(args.immediate, self.registers[args.register_index], 1);
            },
            .store_u16 => {
                const args = i.args.one_register_one_immediate;
                try self.storeMemory(args.immediate, self.registers[args.register_index], 2);
            },
            .store_u32 => {
                const args = i.args.one_register_one_immediate;
                try self.storeMemory(args.immediate, self.registers[args.register_index], 4);
            },
            .store_imm_ind_u8 => {
                const args = i.args.one_register_two_immediates;
                try self.storeMemory(self.registers[args.register_index] +% @as(u32, @bitCast(args.first_immediate)), @intCast(args.second_immediate), 1);
            },
            .store_imm_ind_u16 => {
                const args = i.args.one_register_two_immediates;
                try self.storeMemory(self.registers[args.register_index] +% @as(u32, @bitCast(args.first_immediate)), @intCast(args.second_immediate), 2);
            },
            .store_imm_ind_u32 => {
                const args = i.args.one_register_two_immediates;
                try self.storeMemory(self.registers[args.register_index] +% @as(u32, @bitCast(args.first_immediate)), args.second_immediate, 4);
            },
            .load_imm_jump => {
                const args = i.args.one_register_one_immediate_one_offset;
                self.registers[args.register_index] = args.immediate;
                return try self.branch(args.next_pc);
            },
            .branch_eq_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] == args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_ne_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] != args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_lt_u_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] < args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_le_u_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] <= args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_ge_u_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] >= args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_gt_u_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (self.registers[args.register_index] > args.immediate) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_lt_s_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (@as(i32, @bitCast(self.registers[args.register_index])) < @as(i32, @bitCast(args.immediate))) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_le_s_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (@as(i32, @bitCast(self.registers[args.register_index])) <= @as(i32, @bitCast(args.immediate))) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_ge_s_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (@as(i32, @bitCast(self.registers[args.register_index])) >= @as(i32, @bitCast(args.immediate))) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_gt_s_imm => {
                const args = i.args.one_register_one_immediate_one_offset;
                if (@as(i32, @bitCast(self.registers[args.register_index])) > @as(i32, @bitCast(args.immediate))) {
                    return try self.branch(args.next_pc);
                }
            },
            .sbrk => {
                const args = i.args.two_registers;
                // Implement sbrk behavior here
                // For now, we'll just print a message
                std.debug.print("SBRK called with registers r{} and r{}\n", .{ args.first_register_index, args.second_register_index });
            },
            .store_ind_u8 => {
                const args = i.args.two_registers_one_immediate;
                try self.storeMemory(
                    self.registers[args.second_register_index] +% args.immediate,
                    @truncate(self.registers[args.first_register_index]),
                    1,
                );
            },
            .store_ind_u16 => {
                const args = i.args.two_registers_one_immediate;
                try self.storeMemory(
                    self.registers[args.second_register_index] +% args.immediate,
                    @truncate(self.registers[args.first_register_index]),
                    2,
                );
            },
            .store_ind_u32 => {
                const args = i.args.two_registers_one_immediate;
                try self.storeMemory(
                    self.registers[args.second_register_index] +% args.immediate,
                    self.registers[args.first_register_index],
                    4,
                );
            },
            .load_ind_u8 => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = try self.loadMemory(
                    self.registers[args.second_register_index] +% args.immediate,
                    1,
                );
            },
            .load_ind_i8 => {
                const args = i.args.two_registers_one_immediate;
                const value = try self.loadMemory(
                    self.registers[args.second_register_index] +% args.immediate,
                    1,
                );
                self.registers[args.first_register_index] = @as(
                    u32,
                    @bitCast(
                        @as(
                            i32,
                            @intCast(
                                @as(
                                    i8,
                                    @bitCast(@as(u8, @truncate(value))),
                                ),
                            ),
                        ),
                    ),
                );
            },
            .load_ind_u16 => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = try self.loadMemory(
                    self.registers[args.second_register_index] +% @as(u32, @bitCast(args.immediate)),
                    2,
                );
            },
            .load_ind_i16 => {
                const args = i.args.two_registers_one_immediate;
                const value = try self.loadMemory(self.registers[args.second_register_index] +% @as(u32, @bitCast(args.immediate)), 2);
                self.registers[args.first_register_index] = @as(
                    u32,
                    @bitCast(@as(
                        i32,
                        @intCast(
                            @as(i16, @bitCast(
                                @as(u16, @truncate(value)),
                            )),
                        ),
                    )),
                );
            },
            .load_ind_u32 => {
                const args = i.args.two_registers_one_immediate;
                self.registers[args.first_register_index] = try self.loadMemory(self.registers[args.second_register_index] +% @as(u32, @bitCast(args.immediate)), 4);
            },

            .branch_eq => {
                const args = i.args.two_registers_one_offset;
                if (self.registers[args.first_register_index] == self.registers[args.second_register_index]) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_ne => {
                const args = i.args.two_registers_one_offset;
                if (self.registers[args.first_register_index] != self.registers[args.second_register_index]) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_lt_u => {
                const args = i.args.two_registers_one_offset;
                if (self.registers[args.first_register_index] < self.registers[args.second_register_index]) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_lt_s => {
                const args = i.args.two_registers_one_offset;
                if (@as(
                    i32,
                    @bitCast(self.registers[args.first_register_index]),
                ) < @as(
                    i32,
                    @bitCast(self.registers[args.second_register_index]),
                )) {
                    return try self.branch(args.next_pc);
                }
            },

            .branch_ge_u => {
                const args = i.args.two_registers_one_offset;
                if (self.registers[args.first_register_index] >= self.registers[args.second_register_index]) {
                    return try self.branch(args.next_pc);
                }
            },
            .branch_ge_s => {
                const args = i.args.two_registers_one_offset;
                if (@as(
                    i32,
                    @bitCast(self.registers[args.first_register_index]),
                ) >= @as(
                    i32,
                    @bitCast(self.registers[args.second_register_index]),
                )) {
                    return try self.branch(args.next_pc);
                }
            },

            .load_imm_jump_ind => {
                const args = i.args.two_registers_two_immediates;
                self.registers[args.first_register_index] = args.first_immediate;
                return @intCast(self.registers[args.second_register_index] +% @as(
                    u32,
                    @bitCast(args.second_immediate),
                ));
            },
        }

        // default offset
        return @intCast(i.skip_l() + 1);
    }

    fn branch(self: *PVM, b: u32) !PcOffset {
        const span = trace.span(.branch);
        defer span.deinit();

        span.debug("Attempting branch to address 0x{X:0>8}", .{b});

        // Check if the target address is the start of a basic block
        if (!self.isBasicBlockStart(b)) {
            span.err("Invalid branch target - not a basic block start: 0x{X:0>8}", .{b});
            return error.JumpAddressNotInBasicBlock;
        }

        // Calculate the offset to the target address
        const offset: i32 = @as(i32, @intCast(b)) - @as(i32, @intCast(self.pc));
        span.trace("Branch offset calculated: {d}", .{offset});
        return offset;
    }

    pub fn djump(self: *PVM, a: u32) !PcOffset {
        const span = trace.span(.dynamic_jump);
        defer span.deinit();

        span.debug("Dynamic jump to address 0x{X:0>8}", .{a});

        const halt_pc = 0xFFFF0000;
        const ZA = 2;

        // Check halt condition
        if (a == halt_pc) {
            span.info("Jump to halt address - terminating", .{});
            return error.JumpAddressHalt;
        }

        // Validate jump address
        if (a == 0) {
            span.err("Invalid jump to address 0", .{});
            return error.JumpAddressZero;
        }
        if (a > self.program.jump_table.len() * ZA) {
            span.err("Jump address out of range: 0x{X:0>8}", .{a});
            return error.JumpAddressOutOfRange;
        }
        if (a % ZA != 0) {
            span.err("Jump address not aligned: 0x{X:0>8}", .{a});
            return error.JumpAddressNotAligned;
        }

        // Compute jump destination
        const index = (a / ZA) - 1;
        const jump_dest = self.program.jump_table.getDestination(index);
        span.trace("Jump table lookup - index: {d}, destination: 0x{X:0>8}", .{ index, jump_dest });

        if (std.mem.indexOfScalar(u32, self.program.basic_blocks, jump_dest) == null) {
            span.err("Jump destination not in basic block: 0x{X:0>8}", .{jump_dest});
            return error.JumpAddressNotInBasicBlock;
        }

        const offset = @as(i32, @intCast(@as(i32, @bitCast(jump_dest)) - @as(i32, @bitCast(self.pc))));
        span.debug("Jump offset calculated: {d}", .{offset});
        return offset;
    }

    fn isBasicBlockStart(self: *PVM, address: u32) bool {
        const span = trace.span(.check_basic_block);
        defer span.deinit();

        const is_start = std.mem.indexOfScalar(u32, self.program.basic_blocks, address) != null;
        span.trace("Address 0x{X:0>8} basic block start: {}", .{ address, is_start });
        return is_start;
    }

    fn loadMemory(self: *PVM, address: u32, size: u8) !u32 {
        const span = trace.span(.load_memory);
        defer span.deinit();

        span.debug("Loading {d} bytes from address 0x{X:0>8}", .{ size, address });

        const data = try self.readMemory(address, size);
        var result: u32 = 0;
        var i: u8 = 0;
        while (i < size) : (i += 1) {
            result |= @as(u32, @intCast(data[i])) << @intCast(i * 8);
        }

        span.trace("Loaded value: 0x{X:0>8}", .{result});
        return result;
    }

    pub fn readMemory(self: *PVM, address: u32, size: usize) ![]u8 {
        const span = trace.span(.read_memory);
        defer span.deinit();

        span.debug("Reading {d} bytes from address 0x{X:0>8}", .{ size, address });

        for (self.page_map) |page| {
            if (address >= page.address and address < page.address + page.length) {
                if (address + size > page.address + page.length) {
                    span.err("Memory access out of bounds: address=0x{X:0>8}, size={d}, page_end=0x{X:0>8}", .{ address, size, page.address + page.length });
                    self.error_data = .{ .page_fault = page.address + page.length };
                    return error.MemoryAccessOutOfBounds;
                }

                const offset = address - page.address;
                const data = page.data[offset .. offset + size];
                span.trace("Read successful - offset: {d}, data: {any}", .{ offset, data });
                return data;
            }
        }

        span.err("Page fault at address 0x{X:0>8}", .{address});
        self.error_data = .{ .page_fault = address };
        return error.MemoryPageFault;
    }

    pub fn writeMemory(self: *PVM, address: u32, data: []u8) !void {
        const span = trace.span(.write_memory);
        defer span.deinit();

        span.debug("Writing {d} bytes to address 0x{X:0>8}", .{ data.len, address });

        for (self.page_map) |page| {
            if (address >= page.address and address < page.address + page.length) {
                if (!page.is_writable) {
                    span.err("Write protected memory at address 0x{X:0>8}", .{address});
                    self.error_data = .{ .page_fault = address };
                    return error.MemoryWriteProtected;
                }
                if (address + data.len > page.address + page.length) {
                    span.err("Memory access out of bounds: address=0x{X:0>8}, size={d}, page_end=0x{X:0>8}", .{ address, data.len, page.address + page.length });
                    self.error_data = .{ .page_fault = page.address + page.length };
                    return error.MemoryAccessOutOfBounds;
                }

                const offset = address - page.address;
                std.mem.copyForwards(u8, page.data[offset..], data);
                span.trace("Write successful - offset: {d}, data: {any}", .{ offset, data });
                return;
            }
        }

        span.err("Page fault at address 0x{X:0>8}", .{address});
        self.error_data = .{ .page_fault = address };
        return error.MemoryPageFault;
    }

    fn storeMemory(self: *PVM, address: u32, value: u32, size: u8) !void {
        const span = trace.span(.store_memory);
        defer span.deinit();

        span.debug("Storing value 0x{X:0>8} ({d} bytes) to address 0x{X:0>8}", .{ value, size, address });

        for (self.page_map) |page| {
            if (address >= page.address and address < page.address + page.length) {
                if (!page.is_writable) {
                    span.err("Write protected memory at address 0x{X:0>8}", .{address});
                    self.error_data = .{ .page_fault = address };
                    return error.MemoryWriteProtected;
                }
                if (address + size > page.address + page.length) {
                    span.err("Memory access out of bounds: address=0x{X:0>8}, size={d}, page_end=0x{X:0>8}", .{ address, size, page.address + page.length });
                    self.error_data = .{ .page_fault = page.address + page.length };
                    return error.MemoryAccessOutOfBounds;
                }

                const offset = address - page.address;
                var i: u8 = 0;
                while (i < size) : (i += 1) {
                    const byte = @as(u8, @truncate(value >> @as(u5, @intCast(i * 8))));
                    page.data[offset + i] = byte;
                    span.trace("Wrote byte {d}: 0x{X:0>2} at offset {d}", .{ i, byte, offset + i });
                }
                return;
            }
        }

        span.err("Page fault at address 0x{X:0>8}", .{address});
        self.error_data = .{ .page_fault = address };
        return error.MemoryPageFault;
    }

    pub fn clonePageMap(self: *PVM) ![]PageMap {
        const span = trace.span(.clone_page_map);
        defer span.deinit();

        span.debug("Cloning page map with {d} pages", .{self.page_map.len});

        var cloned_page_map = try self.allocator.alloc(PageMap, self.page_map.len);
        errdefer self.allocator.free(cloned_page_map);

        for (self.page_map, 0..) |page, i| {
            span.trace("Cloning page {d}: address=0x{X:0>8}, length={d}", .{ i, page.address, page.length });

            cloned_page_map[i] = PageMap{
                .address = page.address,
                .length = page.length,
                .is_writable = page.is_writable,
                .data = try self.allocator.alignedAlloc(u8, 8, page.data.len),
            };
            @memcpy(cloned_page_map[i].data, page.data);
        }

        return cloned_page_map;
    }

    pub fn freePageMap(self: *PVM, page_map: []PageMap) void {
        const span = trace.span(.free_page_map);
        defer span.deinit();

        span.debug("Freeing page map with {d} pages", .{page_map.len});

        for (page_map) |page| {
            self.allocator.free(page.data);
        }
        self.allocator.free(page_map);
    }
};
