const std = @import("std");
const uuid = @import("uuid");
const lsquic = @import("lsquic");

const shared = @import("../jamsnp/shared_types.zig");
const Connection = @import("connection.zig").Connection;

const trace = @import("../../tracing.zig").scoped(.network);

pub const StreamId = shared.StreamId;

// --- Internal Stream Struct (for lsquic context)
// This struct holds the state needed *within* the lsquic callbacks.
pub fn Stream(T: type) type {
    return struct {
        id: StreamId,
        connection: *Connection(T),
        lsquic_stream: *lsquic.lsquic_stream_t, // Set in onStreamCreated

        kind: ?shared.StreamKind = null, // Set in onStreamCreated

        // Internal state for reading/writing (managed by lsquic callbacks)
        want_write: bool = false,
        write_buffer: ?[]const u8 = null, // Buffer provided by user via command
        write_buffer_pos: usize = 0,
        owned_write_buffer: bool = false, // Flag to indicate if we own the write buffer memory

        want_read: bool = false,
        read_buffer: ?[]u8 = null, // Buffer provided by user via command
        read_buffer_pos: usize = 0,

        fn create(alloc: std.mem.Allocator, connection: *Connection(T), lsquic_stream: *lsquic.lsquic_stream_t) !*Stream(T) {
            const span = trace.span(.stream_create_internal);
            defer span.deinit();
            span.debug("Creating internal Stream context for connection ID: {}", .{connection.id});
            const stream = try alloc.create(Stream(T));
            errdefer alloc.destroy(stream);

            stream.* = .{
                .id = uuid.v4.new(),
                .lsquic_stream = lsquic_stream,
                .connection = connection,
                .want_write = false,
                .write_buffer = null,
                .write_buffer_pos = 0,
                .want_read = false,
                .read_buffer = null,
                .read_buffer_pos = 0,
            };
            span.debug("Internal Stream context created with ID: {}", .{stream.id});
            return stream;
        }

        pub fn destroy(self: *Stream(T), alloc: std.mem.Allocator) void {
            // Just free the memory, lsquic handles its stream resources.
            const span = trace.span(.stream_destroy_internal);
            defer span.deinit();
            span.debug("Destroying internal Stream struct for ID: {}", .{self.id});

            // Free owned write buffer if it exists
            if (self.owned_write_buffer and self.write_buffer != null) {
                const buffer_to_free = self.write_buffer.?;
                self.connection.owner.allocator.free(buffer_to_free);
                span.debug("Freed owned write buffer during stream destruction for ID: {}", .{self.id});
            }

            // Other buffers are managed by the caller

            self.* = undefined;
            alloc.destroy(self);
        }

        pub fn wantRead(self: *Stream(T), want: bool) void {
            const span = trace.span(.stream_want_read_internal);
            defer span.deinit();
            const want_val: c_int = if (want) 1 else 0;
            span.debug("Setting internal stream want-read to {} for ID: {}", .{ want, self.id });
            _ = lsquic.lsquic_stream_wantread(self.lsquic_stream, want_val);
            // FIXME: handle potential error from lsquic_stream_wantwrite
            self.want_read = want; // Update internal state
        }

        pub fn wantWrite(self: *Stream(T), want: bool) void {
            const span = trace.span(.stream_want_write_internal);
            defer span.deinit();
            const want_val: c_int = if (want) 1 else 0;
            span.debug("Setting internal stream want-write to {} for ID: {}", .{ want, self.id });
            _ = lsquic.lsquic_stream_wantwrite(self.lsquic_stream, want_val);
            // FIXME: handle potential error from lsquic_stream_wantwrite
            self.want_write = want; // Update internal state
        }

        /// Prepare the stream to read into the provided buffer.
        /// Called internally when a Stream read command is processed.
        pub fn setReadBuffer(self: *Stream, buffer: []u8) !void {
            const span = trace.span(.stream_set_read_buffer);
            defer span.deinit();
            span.debug("Setting read buffer (len={d}) for internal stream ID: {}", .{ buffer.len, self.id });

            if (buffer.len == 0) {
                span.warn("Read buffer set with zero-length for stream ID: {}", .{self.id});
                return error.InvalidArgument;
            }
            // Overwrite previous buffer if any? Let's overwrite for simplicity.
            if (self.read_buffer != null) {
                span.warn("Overwriting existing read buffer for stream ID: {}", .{self.id});
            }

            self.read_buffer = buffer;
            self.read_buffer_pos = 0;
            // wantRead should be set by the command handler based on user request
        }

        /// Prepare the stream to write the provided data.
        pub fn setWriteBuffer(self: *Stream(T), data: []const u8) !void {
            const span = trace.span(.stream_set_write_buffer);
            defer span.deinit();
            span.debug("Setting write buffer ({d} bytes) for internal stream ID: {}", .{ data.len, self.id });

            if (data.len == 0) {
                span.warn("Write buffer set with zero-length data for stream ID: {}. Ignoring.", .{self.id});
                return error.ZeroDataLen;
            }
            // Let's overwrite for simplicity.
            if (self.write_buffer != null) {
                span.err("Stream ID {} is already writing, cannot issue new write.", .{self.id});
                return error.StreamAlreadyWriting;
            }

            self.write_buffer = data;
            self.write_buffer_pos = 0;
            self.owned_write_buffer = false;
            // wantWrite should be set by the command handler
        }

        /// Prepare the stream to write the provided data, but duplicate the data so the stream owns it.
        /// The duplicated data will be freed when the stream is done with it.
        pub fn setOwnedWriteBuffer(self: *Stream(T), data: []const u8) !void {
            const span = trace.span(.stream_set_owned_write_buffer);
            defer span.deinit();
            span.debug("Setting owned write buffer ({d} bytes) for internal stream ID: {}", .{ data.len, self.id });

            if (data.len == 0) {
                span.warn("Owned write buffer set with zero-length data for stream ID: {}. Ignoring.", .{self.id});
                return error.ZeroDataLen;
            }

            if (self.write_buffer != null) {
                span.err("Stream ID {} is already writing, cannot issue new write.", .{self.id});
                return error.StreamAlreadyWriting;
            }

            // Duplicate the data
            const duped_data = try self.connection.owner.allocator.dupe(u8, data);

            self.write_buffer = duped_data;
            self.write_buffer_pos = 0;
            self.owned_write_buffer = true;
            // wantWrite should be set by the command handler
        }

        pub fn flush(self: *Stream(T)) !void {
            const span = trace.span(.stream_flush_internal);
            defer span.deinit();
            span.debug("Flushing internal stream ID: {}", .{self.id});
            if (lsquic.lsquic_stream_flush(self.lsquic_stream) != 0) {
                span.err("Failed to flush internal stream ID: {}", .{self.id});
                return error.StreamFlushFailed;
            }
        }

        pub fn shutdown(self: *Stream(T), how: c_int) !void {
            const span = trace.span(.stream_shutdown_internal);
            defer span.deinit();
            const direction = switch (how) {
                0 => "read",
                1 => "write",
                2 => "read and write",
                else => "unknown",
            };
            span.debug("Shutting down internal stream ID {} ({s} side)", .{ self.id, direction });
            if (lsquic.lsquic_stream_shutdown(self.lsquic_stream, how) != 0) {
                span.err("Failed to shutdown internal stream ID {}: {s}", .{ self.id, direction });
                return error.StreamShutdownFailed;
            }
        }

        pub fn close(self: *Stream(T)) !void {
            const span = trace.span(.stream_close_internal);
            defer span.deinit();
            span.debug("Closing internal stream ID: {}", .{self.id});
            // This signals intent to close; onStreamClosed callback handles final cleanup.
            if (lsquic.lsquic_stream_close(self.lsquic_stream) != 0) {
                span.err("Failed to close internal stream ID: {}", .{self.id});
                return error.StreamCloseFailed;
            }
        }

        // -- LSQUIC Stream Callbacks
        pub fn onClientStreamCreated(
            _: ?*anyopaque, // ea_stream_if_ctx (unused)
            maybe_lsquic_stream: ?*lsquic.lsquic_stream_t,
        ) callconv(.C) *lsquic.lsquic_stream_ctx_t {
            const span = trace.span(.on_stream_created);
            defer span.deinit();
            span.debug("LSQUIC stream created callback received", .{});

            // FIXME: we should check the edge case where maybe_lsquic_stream is null
            // in this case the connection was closed before we could create the stream
            // and we should not create a stream context. and we should indicate this on the callback

            // Get the parent Connection context
            const lsquic_connection = lsquic.lsquic_stream_conn(maybe_lsquic_stream);
            const conn_ctx = lsquic.lsquic_conn_get_ctx(lsquic_connection).?; // Assume parent conn context is valid
            const connection: *Connection(T) = @alignCast(@ptrCast(conn_ctx));

            // Use the internal Stream.create
            const stream = Stream(T).create(connection.owner.allocator, connection, maybe_lsquic_stream orelse unreachable) catch |err| {
                std.debug.panic("OutOfMemory creating internal Stream context: {s}", .{@errorName(err)});
            };

            // Add stream to the client's map
            connection.owner.streams.put(stream.id, stream) catch |err| {
                std.debug.panic("OutOfMemory adding stream to map: {s}", .{@errorName(err)});
            };

            // Invoke the user-facing callback via the client
            shared.invokeCallback(&connection.owner.callback_handlers, .StreamCreated, .{
                .StreamCreated = .{
                    .connection = connection.id,
                    .stream = stream.id,
                },
            });

            // Return our internal stream struct pointer as the context for lsquic
            return @ptrCast(stream);
        }

        pub fn onServerStreamCreated(
            ea_stream_if_ctx: ?*anyopaque, // ea_stream_if_ctx (unused)
            maybe_lsquic_stream: ?*lsquic.lsquic_stream_t,
        ) callconv(.C) *lsquic.lsquic_stream_ctx_t {
            const span = trace.span(.on_stream_created_server);
            defer span.deinit();

            const ctx = onClientStreamCreated(ea_stream_if_ctx, maybe_lsquic_stream);
            // We need to set the stream to read mode to get any data from client
            if (lsquic.lsquic_stream_wantread(maybe_lsquic_stream, 1) != 0) {
                span.err("Failed to set stream to read mode for stream", .{});
            }

            return ctx;
        }

        pub fn onStreamReadServer(
            maybe_lsquic_stream: ?*lsquic.lsquic_stream_t,
            maybe_stream_ctx: ?*lsquic.lsquic_stream_ctx_t,
        ) callconv(.C) void {
            const span = trace.span(.on_stream_read_server);
            defer span.deinit();

            const stream_ctx = maybe_stream_ctx orelse {
                span.err("onStreamReadServer called with null context!", .{});
                return;
            };
            const stream: *Stream(T) = @alignCast(@ptrCast(stream_ctx)); // This is the internal Stream
            span.debug("onStreamReadServer triggered for internal stream ID: {}", .{stream.id});

            // Check if a read buffer has been provided via command
            if (stream.kind == null) {
                var kind_buffer: u8 = undefined;
                const read_size = lsquic.lsquic_stream_read(maybe_lsquic_stream, @ptrCast(&kind_buffer), 1);
                if (read_size == 1) {
                    stream.kind = shared.StreamKind.fromRaw(kind_buffer) catch {
                        span.err("Invalid stream kind read for stream ID {}: {d}", .{ stream.id, kind_buffer });
                        stream.close() catch |err| std.debug.panic("Failed to close stream: {s}", .{@errorName(err)});
                        return;
                    };
                    span.debug("Stream kind set to: {}", .{stream.kind.?});
                    // We got our kind, no need to read again, this is the responsibility of the
                    // protocol which will get activated based on the kind
                    stream.wantRead(false);

                    shared.invokeCallback(&stream.connection.owner.callback_handlers, .ServerStreamCreated, .{
                        .ServerStreamCreated = .{
                            .connection = stream.connection.id,
                            .stream = stream.id,
                            .kind = stream.kind.?,
                        },
                    });
                    return;
                } else if (read_size == 0) {
                    span.warn("No data read for stream ID {}. Stream kind not set. Try again", .{stream.id});
                    return;
                } else {
                    span.err("Error reading stream kind for stream ID: {}", .{stream.id});
                    stream.close() catch |err| std.debug.panic("Failed to close stream: {s}", .{@errorName(err)});
                    return;
                }
            }

            // When we have already a kind, we can call the onStreamRead
            onStreamRead(maybe_lsquic_stream, maybe_stream_ctx);
        }

        pub fn onStreamRead(
            maybe_lsquic_stream: ?*lsquic.lsquic_stream_t,
            maybe_stream_ctx: ?*lsquic.lsquic_stream_ctx_t,
        ) callconv(.C) void {
            const span = trace.span(.on_stream_read);
            defer span.deinit();

            const stream_ctx = maybe_stream_ctx orelse {
                span.err("onStreamRead called with null context!", .{});
                return;
            };
            const stream: *Stream(T) = @alignCast(@ptrCast(stream_ctx)); // This is the internal Stream
            span.debug("onStreamRead triggered for internal stream ID: {}", .{stream.id});

            // Check if a read buffer has been provided via command
            if (stream.read_buffer == null) {
                span.warn("onStreamRead called for stream ID {} but no read buffer set via command. Disabling wantRead.", .{stream.id});
                stream.wantRead(false);
                return;
            }

            const buffer_available = stream.read_buffer.?[stream.read_buffer_pos..];
            if (buffer_available.len == 0) {
                span.warn("onStreamRead called for stream ID {} but read buffer is full.", .{stream.id});
                stream.wantRead(false);
                return;
            }

            const read_size = lsquic.lsquic_stream_read(maybe_lsquic_stream, buffer_available.ptr, buffer_available.len);

            if (read_size == 0) {
                span.debug("End of stream reached for stream ID: {}", .{stream.id});
                shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataEndOfStream, .{
                    .DataEndOfStream = .{
                        .connection = stream.connection.id,
                        .stream = stream.id,
                        .data_read = stream.read_buffer.?[0..stream.read_buffer_pos],
                    },
                });
                stream.read_buffer = null;
                stream.read_buffer_pos = 0;
                stream.wantRead(false);
            } else if (read_size < 0) {
                switch (std.posix.errno(read_size)) {
                    std.posix.E.AGAIN => {
                        span.debug("Read would block for stream ID: {}", .{stream.id});
                        shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataWouldBlock, .{
                            .DataWouldBlock = .{
                                .connection = stream.connection.id,
                                .stream = stream.id,
                            },
                        });
                        // Keep wantRead true
                    },
                    else => |err| {
                        span.err("Error reading from stream ID {}: {s}", .{ stream.id, @tagName(err) });
                        shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataReadError, .{
                            .DataReadError = .{
                                .connection = stream.connection.id,
                                .stream = stream.id,
                                .error_code = @intFromEnum(err),
                            },
                        });
                        stream.read_buffer = null;
                        stream.read_buffer_pos = 0;
                        stream.wantRead(false);
                    },
                }
            } else { // read_size > 0
                const bytes_read: usize = @intCast(read_size);
                span.debug("Read {d} bytes from stream ID: {}", .{ bytes_read, stream.id });

                const prev_pos = stream.read_buffer_pos;
                stream.read_buffer_pos += bytes_read;
                const data_just_read = stream.read_buffer.?[prev_pos..stream.read_buffer_pos];

                shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataReceived, .{
                    .DataReceived = .{
                        .connection = stream.connection.id,
                        .stream = stream.id,
                        .data = data_just_read,
                    },
                });

                if (stream.read_buffer_pos == stream.read_buffer.?.len) {
                    span.debug("User read buffer full for stream ID: {}. Disabling wantRead.", .{stream.id});
                    stream.read_buffer = null;
                    stream.read_buffer_pos = 0;
                    stream.wantRead(false);
                    // TODO: Signal buffer full? Or rely on DataReceived?
                }
            }
        }

        pub fn onStreamWrite(
            maybe_lsquic_stream: ?*lsquic.lsquic_stream_t,
            maybe_stream_ctx: ?*lsquic.lsquic_stream_ctx_t,
        ) callconv(.C) void {
            const span = trace.span(.on_stream_write);
            defer span.deinit();

            _ = maybe_lsquic_stream; // Unused in this context

            const stream_ctx = maybe_stream_ctx orelse {
                span.err("onStreamWrite called with null context!", .{});
                return;
            };
            const stream: *Stream(T) = @alignCast(@ptrCast(stream_ctx)); // Internal Stream
            span.debug("onStreamWrite triggered for internal stream ID: {}", .{stream.id});

            if (stream.write_buffer == null) {
                span.warn("onStreamWrite called for stream ID {} but no write buffer set via command. Disabling wantWrite.", .{stream.id});
                stream.wantWrite(false);
                return;
            }

            const data_to_write = stream.write_buffer.?[stream.write_buffer_pos..];
            const total_size = stream.write_buffer.?.len;

            if (data_to_write.len == 0) {
                span.warn("onStreamWrite called for stream ID {} but write buffer position indicates completion.", .{stream.id});
                stream.wantWrite(false);
                return;
            }

            const written = lsquic.lsquic_stream_write(stream.lsquic_stream, data_to_write.ptr, data_to_write.len);

            if (written == 0) {
                span.trace("No data written to stream ID {} (likely blocked)", .{stream.id});
                // Keep wantWrite true
                return;
            } else if (written < 0) {
                if (std.posix.errno(written) == std.posix.E.AGAIN) {
                    span.trace("Stream write would block (EAGAIN) for stream ID {}", .{stream.id});
                    // Keep wantWrite true
                    return;
                } else {
                    const err_code = -written;
                    span.err("Stream write failed for stream ID {} with error code: {d}", .{ stream.id, err_code });
                    shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataWriteError, .{
                        .DataWriteError = .{
                            .connection = stream.connection.id,
                            .stream = stream.id,
                            .error_code = @intCast(err_code),
                        },
                    });
                    stream.write_buffer = null;
                    stream.write_buffer_pos = 0;
                    stream.wantWrite(false);
                    return;
                }
            }

            // written > 0
            const bytes_written: usize = @intCast(written);
            span.debug("Written {d} bytes to stream ID: {}", .{ bytes_written, stream.id });
            stream.write_buffer_pos += bytes_written;

            shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataWriteProgress, .{
                .DataWriteProgress = .{
                    .connection = stream.connection.id,
                    .stream = stream.id,
                    .bytes_written = stream.write_buffer_pos,
                    .total_size = total_size,
                },
            });

            if (stream.write_buffer_pos >= total_size) {
                span.debug("Write complete for user buffer (total {d} bytes) on stream ID: {}", .{ total_size, stream.id });

                shared.invokeCallback(&stream.connection.owner.callback_handlers, .DataWriteCompleted, .{
                    .DataWriteCompleted = .{
                        .connection = stream.connection.id,
                        .stream = stream.id,
                        .total_bytes_written = stream.write_buffer_pos,
                    },
                });

                // Free the buffer if we own it
                if (stream.owned_write_buffer and stream.write_buffer != null) {
                    const buffer_to_free = stream.write_buffer.?;
                    stream.connection.owner.allocator.free(buffer_to_free);
                    span.debug("Freed owned write buffer for stream ID: {}", .{stream.id});
                }

                stream.write_buffer = null;
                stream.write_buffer_pos = 0;
                stream.owned_write_buffer = false;
                span.trace("Disabling write interest for stream ID {}", .{stream.id});
                stream.wantWrite(false);

                // Flush the stream to ensure all data is sent
                span.debug("Flushing stream ID {} after write completion", .{stream.id});
                if (lsquic.lsquic_stream_flush(stream.lsquic_stream) != 0) {
                    span.err("Failed to flush stream ID {} after write completion", .{stream.id});
                }
            }
            // else: Keep wantWrite true
        }

        pub fn onStreamClosed(
            _: ?*lsquic.lsquic_stream_t,
            maybe_stream_ctx: ?*lsquic.lsquic_stream_ctx_t,
        ) callconv(.C) void {
            const span = trace.span(.on_stream_closed);
            defer span.deinit();
            span.debug("LSQUIC stream closed callback received", .{});

            const stream_ctx = maybe_stream_ctx orelse {
                span.err("onStreamClosed called with null context!", .{});
                return;
            };
            const stream: *Stream(T) = @alignCast(@ptrCast(stream_ctx)); // Internal Stream
            span.debug("Processing internal stream closure for ID: {}", .{stream.id});

            // Invoke the user-facing callback
            shared.invokeCallback(&stream.connection.owner.callback_handlers, .StreamClosed, .{
                .StreamClosed = .{
                    .connection = stream.connection.id,
                    .stream = stream.id,
                },
            });

            // Remove the internal stream from the client's map
            if (stream.connection.owner.streams.fetchRemove(stream.id)) |_| {
                span.debug("Removed internal stream ID {} from map.", .{stream.id});
            } else {
                span.warn("Closing an internal stream (ID: {}) that was not found in the map.", .{stream.id});
            }

            // Destroy our internal stream context struct
            const id = stream.id;
            stream.destroy(stream.connection.owner.allocator);

            span.debug("Internal stream cleanup complete for formerly ID: {}", .{id});
        }
    };
}
